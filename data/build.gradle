apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: 'https://raw.githubusercontent.com/mcrafts/dot-gradles/master/test-report.gradle'

android {
  compileSdkVersion buildVersion.targetSdk

  defaultConfig {
    minSdkVersion buildVersion.minSdk
    targetSdkVersion buildVersion.targetSdk
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    buildConfigField "String", "UNSPLASH_TOKEN", "\"" + getUnsplashToken() + "\""
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  implementation project(":domain")

  implementation deps.kotlin.stdlib

  implementation deps.support.appCompat

  api deps.room.runtime
  kapt deps.room.compiler
  api deps.room.rxJava

  api deps.retrofit.runtime
  api deps.retrofit.gson
  api deps.retrofit.rxJava
  api deps.okhttpLogging

  implementation deps.rxJava
  implementation deps.rxAndroid
}

def getUnsplashToken() {
  Properties properties = new Properties()
  properties.load(project.rootProject.file('local.properties').newDataInputStream())
  return properties.getProperty("unsplashToken")
}